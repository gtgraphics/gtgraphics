# TODO Allow multiple Tabs

queryRemoveButtonState = ($tabs) ->
  disabled = $tabs.find('.translation-tab.translation-available').length <= 1
  $tabs.find('.remove-translation').prop('disabled', disabled)

queryAddButtonStates = ($tabs) ->
  $addableItems = $tabs.find('.add-translation')
  $addableItems.each ->
    $item = $(@).closest('li')
    if translationAdded($tabs, $item.data('locale'))
      $item.addClass('disabled')
    else
      $item.removeClass('disabled')

queryState = ($tabs) ->
  queryRemoveButtonState($tabs)
  queryAddButtonStates($tabs)

translationAdded = ($tabs, locale) ->
  added = false
  $tabs.find('.translation-tab.translation-available').each ->
    if $(@).data('locale') == locale
      added = true
      return false
  added

initAvailableTabs = ($tabs) ->
  $target = $('#translations')

  $tabPaneWithError = null

  definedLocales = []
  $target.find('.tab-pane[data-locale]').each ->
    $tabPane = $(@)
    if $tabPane.find('.destroy-translation').val() != 'true'
      definedLocales.push($tabPane.data('locale'))

  $tabs.find('.translation-tab').each ->
    $tab = $(@)
    locale = $tab.data('locale')
    if jQuery.inArray(locale, definedLocales) >= 0
      $tab.show()
      $tab.addClass('translation-available').removeClass('translation-unavailable')
    else
      $tab.hide()
      $tab.removeClass('translation-available').addClass('translation-unavailable')

    #$tabPaneErrors = $tabPane.find('.form-group.has-error')  
    $tabPane = $target.find(".tab-pane[data-locale='#{locale}']")
    $tabPaneErrors = $tabPane.find('.form-group.has-error')
    if $tabPaneErrors.length > 0 and !$tabPaneWithError
      $tabPaneWithError = $tabPane

  if $tabPaneWithError
    locale = $tabPaneWithError.data('locale')
    $tabs.find(".translation-tab[data-locale='#{locale}'] a").tab('show')
  else
    $availableTranslationTabs = $('.translation-tab.translation-available')
    if $availableTranslationTabs.filter('.active').length == 0
      locale = $availableTranslationTabs.first().data('locale')
      $tabs.find(".translation-tab[data-locale='#{locale}'] a").tab('show')

createTranslation = ($tabs, locale, callback) ->
  $target = $('#translations')
  $tabPane = $target.find(".tab-pane[data-locale='#{locale}']")
  translationLoaded = $tabPane.length > 0
  if translationLoaded
    $tabs.find(".translation-tab[data-locale='#{locale}'] a").tab('show')    
    $tabPane.find(".destroy-translation").val(false)
    callback(true) if callback
  else
    url = $tabs.data('url')
    jQuery.ajax
      url: url,
      type: 'get',
      data: { translated_locale: locale },
      dataType: 'html',
      success: (html) ->
        $tabPane = $(html).appendTo($target)
        $tabPane.find('.editor').editor()
        callback(true) if callback
      error: ->
        callback(false) if callback


$.fn.translationTabs = ->
  @find('#translations_tabs').each ->
    $tabs = $(@)
    initAvailableTabs($tabs)
    queryState($tabs)

    # Tab Actions
    $tabs.find('.translation-tab a').click (event) ->
      #return unless event.currentTarget == @
      event.preventDefault()
      $link = $(@)
      $tab = $link.closest('.translation-tab')
      unless $tab.hasClass('disabled')
        $link.tab('show')

    # Remove Translation
    $tabs.on 'click', '.remove-translation', (event) ->
      #return unless event.currentTarget == @
      event.preventDefault()
      event.stopPropagation()
      $(document).click() # closes all dropdowns

      $link = $(@)
      $tab = $link.closest('.translation-tab')
      if $tab.hasClass('active')
        $neighborTab = $tab.prev('.translation-tab').add($tab.next('.translation-tab')).first()
        $neighborTab.find('a').tab('show') 
      $tab.hide().removeClass('translation-available').addClass('translation-unavailable')
      $link.tooltip('hide') # dirty fix

      locale = $tab.data('locale')

      $tabPane = $("#translations .tab-pane[data-locale='#{locale}']")
      $tabPane.find(".destroy-translation").val(true)

      queryState($tabs)

    # Add Translation
    $tabs.find('.add-translation').click (event) ->
      event.preventDefault()
      $link = $(@)
      $dropdownItem = $link.closest('li')
      unless $dropdownItem.hasClass('disabled')
        locale = $link.data('locale')

        $dropdownItem.addClass('disabled')

        $tabs.trigger('loading.translation')

        # Show new Tab
        $tab = $tabs.find(".translation-tab[data-locale='#{locale}']")
        $tab.show().addClass('translation-available').removeClass('translation-unavailable')

        # Replace Flag With Loader
        $flag = $tab.find('.flag')
        $flag.hide()

        $loader = $('<div />', class: 'loader loader-tabs')
        $('<i />', class: 'fa fa-refresh fa-spin').appendTo($loader)
        $loader.insertAfter($flag)

        $tab.addClass('disabled')

        # Load Translation Tab
        createTranslation $tabs, locale, (created) ->
          #$flag.attr('src', flagIconSrc)
          $flag.show()
          $loader.remove()
          if created
            $tab.removeClass('disabled')
            $tabs.find(".translation-tab[data-locale='#{locale}'] a").tab('show')
          else
            $tab.hide().addClass('translation-unavailable').removeClass('translation-available')
            alert(I18n.translate('translations.error'))
          queryState($tabs)
          $tabs.trigger('loaded.translation')

$.prepare ->
  @translationTabs()